# 2.2 变量

NLang 中的变量用于存储数据并在脚本执行过程中被读取或修改。本章将介绍如何声明变量、使用常量、理解作用域以及相关的最佳实践。

## 声明变量

在 NLang 中，可以通过 `set` 关键字来声明变量并为其赋值：

```javascript
set myVar 42 // initialize variable with number
```

- `myVar` 为变量名，遵循"字母、数字、下划线且不能以数字开头"的命名规则。
- 变量首次出现前读取会获得`null`值。

### 多变量声明

一次 `set` 指令可以声明并赋值多个变量：

```javascript
set x 1,
    y 2,
    z 3 // declare three variables at once
```

### 解构赋值

NLang 支持数组与对象的解构赋值，使代码更加简洁：

```javascript
set [a, b] [1, 2] // a = 1, b = 2
set {name, age} {name: "John", age: 20} // name = "John", age = 20
```

## 常量

使用 `const` 关键字可以声明常量。常量在创建后不可重新赋值：

```javascript
const PI 3.14159 // immutable value
```

尝试修改常量会抛出错误：

```javascript
set PI 3 // Error: Assignment to constant variable "PI"
```

## 变量作用域

变量的**作用域**决定了它在何处可见。

### 全局作用域

默认情况下，在脚本顶层通过 `set` 或 `const` 声明的变量属于全局作用域：

```javascript
set globalVar "hello" // global scope
```

全局变量可在任何地方被访问。

### 场景作用域

在场景内部通过 `local set` 声明的变量只在该场景内可见：

```javascript
local set name "Narra"
name // "Narra"
```

当场景作用域有一个和全局变量同名的变量时，会优先读取场景作用域的变量。

```javascript
set name "global"
local set name "scene"

name // "scene"
``` 

值得注意的是，如下的情况中，全局作用域的优先级更高：  
```javascript
set name "global"

name // "global"

local set name "scene"
```

这是因为尝试从场景中获取变量时，如果当前变量从未被设置过，会从全局作用域中获取。

> [!NOTE]
> 场景作用域的变量在场景被销毁时会自动被销毁。

## 重新赋值

对已声明的变量可以直接进行重新赋值，而无需再次使用 `set`：

```javascript
set counter 0
set counter counter + 1
```

但是下面的语句可能带来可读性问题：  
```javascript
set counter 5

scene Scene {
    local set counter counter + 1

    counter // 6
}

counter // 5
```

这是为什么？这是因为当使用`set counter counter + 1`时，右侧表达式中的counter会读取全局作用域中的counter并且加一。  
而在作用域之外读取counter不会获得场景作用域内的值，而是全局作用域中的。

## 字符串插值

当变量置于大括号中时，NLang 会在字符串模板里插入其值：

```javascript
set name "Alice"
John: Hello, {name}! // output: Hello, Alice!
```

### 插值表达式

插值中的表达式会在运行到当前行或当前字符串字面量表达式被评估时执行。

```javascript
set money 100

John: You still need {500 - money} dollars to buy this.
```

## 空值与默认值

未初始化的变量读取时将得到 `null`：

```javascript
score // null
```

可使用逻辑运算符为 `null` 值提供默认值：

```javascript
set finalScore score ?? 0 // if score is null, use 0
```

关于`??`运算符的更多信息，请参考[运算符](./3.%20运算符.md)。

## 下一步

下一章节：[运算符](./3.%20运算符.md)
